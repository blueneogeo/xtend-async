// SETUP BUILDSCRIPT ///////////////////////////////////////////////////////////

buildscript {

  repositories {
    jcenter()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
    maven {
      url "${artifactory_url}/plugins-release"
      credentials {
        username artifactory_user
        password artifactory_password
      }
    }
  }

  dependencies {
    classpath 'org.xtext:xtext-gradle-plugin:1.0.15'
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
  }
  
}

task wrapper(type: Wrapper) {
		gradleVersion = '2.14'
}

// CONFIGURE ALL SUBPROJECTS ///////////////////////////////////////////////////

subprojects {
  apply plugin: 'eclipse'
  apply plugin: 'nebula.provided-base'
  apply plugin: 'java'
  apply plugin: 'org.xtext.xtend'
  apply plugin: 'maven-publish'
  apply plugin: 'jacoco'

  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion

  eclipse.project.name = rootProject.name + '-' + project.name
  tasks.eclipse.dependsOn(cleanEclipse)

  xtend { preferences = [ 'useJavaCompilerCompliance':true ] }

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url "${artifactory_url}/main"
      credentials {
        username artifactory_user
        password artifactory_password
      }
    }
  }

  configurations {
    quasar
  }

  dependencies {
    quasar "co.paralleluniverse:quasar-core:${quasarVersion}@jar"
    provided "co.paralleluniverse:quasar-core:${quasarVersion}"
    compile "org.eclipse.xtend:org.eclipse.xtend.lib:${xtendVersion}"
    testCompile "junit:junit:${junitVersion}"
  }

  task sourcesJar( type: Jar, dependsOn: classes ) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar( type: Jar, dependsOn: javadoc ) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  tasks.withType( Javadoc ) {
    if ( JavaVersion.current().isJava8Compatible() ) {
      options.addStringOption( 'Xdoclint:none', '-quiet' )
    }
  }

  tasks.withType(Test) {
    jvmArgs "-javaagent:${configurations.quasar.singleFile}=b" // =v, =d =b
    jvmArgs "-Dco.paralleluniverse.fibers.verifyInstrumentation=true"
    jvmArgs "-Dco.paralleluniverse.fibers.allowJdkInstrumentation=true"
  }
    
  test {
      testLogging {
          error {
            showStackTraces true
            exceptionFormat "full"
          }
          events "passed", "skipped", "failed", "standardOut", "standardError"
      }
  }

  task install( dependsOn: publishToMavenLocal )

  jar {
    baseName rootProject.name + '-' + project.name
  }

  jacoco {
    toolVersion = '0.7.6.201602180812'
  }

  jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports.html.enabled true
  }

  publishing {
    publications {
      mavenJava( MavenPublication ) {
        artifactId rootProject.name + '-' + project.name
        from components.java
        artifact sourcesJar
        artifact javadocJar
        pom.withXml {
          asNode().dependencies.'*'.each() {
            it.scope*.value = 'compile'
          }
        }
      }
    }
    repositories {
      maven {
        url "${artifactory_url}/main-local"
        credentials {
          username artifactory_user
          password artifactory_password
        }
      }
    }
  }
}

project('annotations') {
  dependencies {
    compile 'com.kimengi.util:xtend-tools-core:latest.release'
  }
}

project('core') {
  dependencies {
    compile project(':annotations')
    testCompile 'com.kimengi.util:xtend-tools-test:latest.release'
  }
}

project('event') {
  dependencies {
    compile project(':core')
  }
}

project('rx') {
  dependencies {
    compile project(':core')
    compile 'io.reactivex:rxjava:1.1.3'
    testCompile 'com.kimengi.util:xtend-tools-test:latest.release'
  }
}

project('collections') {
    dependencies {
        compile project(':core')
        compile project(':event')
        testCompile 'com.kimengi.util:xtend-tools-test:latest.release'
    }
}


project( 'fibers' ) {

    dependencies {
      compile project( ':core' )
      compile 'com.kimengi.util:xtend-tools-annotations:latest.release'
      testCompile 'com.kimengi.util:xtend-tools-test:latest.release'
    }

    tasks.withType(Test) {
      // jvmArgs "-javaagent:${configurations.quasar.singleFile}" // =v, =d
      jvmArgs "-Dco.paralleluniverse.fibers.verifyInstrumentation=true"
      jvmArgs "-Dco.paralleluniverse.fibers.allowJdkInstrumentation=true"
    }

    task serviceAgent {
      description 'Prints the Eclipse VM parameters necessary to run JUnit tests'
      doLast {
        println "-javaagent:${configurations.quasar.asPath}"
      }
    }

}

// scan and instrument java files for suspendable code (for quasar fiber support)
def scanAndInstrument(project, sset, updateSuspendables, configs) {

    def cp = '' + sset.output.classesDir + ':' + sset.output.resourcesDir + ':' + configs*.asPath.join(':')

    ant.taskdef(
        name:'scanSuspendables', classname:'co.paralleluniverse.fibers.instrument.SuspendablesScanner',
        classpath: cp
    )
    ant.scanSuspendables(
        auto: (updateSuspendables == 'true'),
        suspendablesFile: "$sset.output.resourcesDir/META-INF/suspendables",
        supersFile:"$sset.output.resourcesDir/META-INF/suspendable-supers",
        append: true) {
        fileset(dir: sset.output.classesDir)
    }

    if(updateSuspendables == 'true') {
      println('Copying suspendables and suspendable-supers to Java META-INF dir (property updateSuspendables).')
      copy {
          from "$sset.output.resourcesDir/META-INF"
          into "$project.projectDir/src/main/java/META-INF"
          include 'suspendables'
          include 'suspendable-supers'
      }
    }

    ant.taskdef(name:'instrumentation', classname:'co.paralleluniverse.fibers.instrument.InstrumentationTask',
        classpath: cp)
    ant.instrumentation(verbose:'true', check:'true', debug:'true') {
        fileset(dir: sset.output.classesDir) {
            exclude(name: 'co/paralleluniverse/fibers/instrument/*.class')
        }
    }
}
